; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define CurrDate GetDateTimeString('yymmddhhnn', '', '')

#define MCVersion '1.21.9'
#define Enable_IME_Chat 1
#define Enable_MAP_Xaeros 1
#define Enable_MAP_journey 1
#define Enable_VOICE_chat 1
#define Enable_RENDER_Sodium 1
#define Enable_RENDER_Iris 1
#define Enable_MOD_Menu 1
#define Enable_SHADER_Files 1
#define Enable_UI_BetterF3 0
#define Enable_PERF_Lithium 1
#define Enable_FIX_Memoryleak 0
#define Enable_BypassResource 1
#define Enable_Litematica 0
#define Enable_HoldMyItem 0
#define Enable_LambDynamic 1

; Fabric
#define JarFabricInstaller 'fabric-installer-1.1.0'
#define JarFabricAPI 'fabric-api-0.134.0+1.21.9'

; Map
#if Enable_MAP_Xaeros!=0
#define JarXaerosMinimap 'Xaeros_Minimap_25.2.14_Fabric_1.21.9'
#define JarXaerosWorldmap 'XaerosWorldMap_1.39.17_Fabric_1.21.9'
#endif

#if Enable_MAP_journey!=0
#define JarJourneymap 'journeymap-fabric-1.21.9-6.0.0-beta.52'
#endif

#if Enable_UI_BetterF3!=0
#define JarBetterF3 'BetterF3-15.0.0-Fabric-1.21.6'
#endif

#if Enable_MOD_Menu!=0
#define JarModMenu 'modmenu-16.0.0-rc.1'
#endif

#if Enable_VOICE_chat!=0
#define JarVoicechat 'voicechat-fabric-1.21.9-2.6.4'
#endif

#if Enable_IME_Chat!=0
; IME
;#define JarCaramelchat 'caramelChat-mc1.20.4-fabric-1.2.0'
#define JarKoreanIME 'koreanpatch-fabric-1.9.0+mc1.21.9'
#endif

; performance
#if Enable_PERF_Lithium!=0
#define JarLithium 'lithium-fabric-0.19.1+mc1.21.9'
#endif
#define JarMoreculling 'moreculling-fabric-1.21.4-1.2.9'

; memory leak fix
#if Enable_FIX_Memoryleak!=0
#define JarMemoryleakfix 'memoryleakfix-fabric-1.17+-1.1.5'
#endif

; iris shader
#if Enable_RENDER_Sodium!=0
#define JarSodium 'sodium-fabric-0.7.0+mc1.21.9'
#endif

#if Enable_RENDER_Iris!=0
#define JarIris 'iris-fabric-1.9.3+mc1.21.9'
#endif

#if Enable_BypassResource!=0
#define JarBypassResource 'EpicForcedResourcePackBypassMod-1.7.1'
#endif

#if Enable_Litematica!=0
#define JarMalilib 'malilib-fabric-1.21.8-0.25.5'
#define JarLitematica 'litematica-fabric-1.21.8-0.23.4'
#endif

#if Enable_HoldMyItem!=0
#define JarHoldMyItem 'HMI 5.0L4 1.21.6+'
#endif

#if Enable_LambDynamic!=0
#define JarLambDynamic 'lambdynamiclights-4.7.2+1.21.9'
#endif

#define JarVulkan 'VulkanMod_1.21.4-0.5.4'


#if Enable_SHADER_Files!=0
; Shaders
#define ZipShaderSEUS 'SEUS-Renewed-v1.0.1'
#define ZipShaderBuilder 'Builder''s Modded Shaders V2.11.0'
#define ZipShaderSildur 'Sildur''s+Vibrant+Shaders+v1.50+Lite'
#define ZipShaderMakeup 'MakeUp-UltraFast-8.6g'
#define ZipShaderBSL 'BSL_v8.2.01'
#define ZipShaderSolas 'Solas Shader V2.0 [BETA 0.6b]'
#define ZipShaderNoble 'Noble v1.5.6'
#define ZipShaderVoyager 'Voyager shader 2.0.7'
#define ZipShaderBliss 'Bliss-Shader-dev'
#define ZipShaderBeyond 'BeyondBeliefLegacy_V1.2.3'
#define ZipShaderOpal 'OPAL_v1.0.0'
#define ZipShaderComplementary 'ComplementaryUnbound_r5.5.1'
#endif

; ======================================================================

#define DefaultRuntime '{commonpf32}\Minecraft Launcher'

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{55E52D9A-F129-498C-BFD2-D7BD042CBD79}
AppName={#MCVersion}|{#JarIris}|{#JarSodium}|{#CurrDate}
AppVersion=0.99a
;AppVerName=OptiFine Installer {#JarName2}
AppPublisher=anon
OutputBaseFilename=Fabric_{#MCVersion}_{#JarIris}_{#CurrDate}
Compression=lzma2/max
SolidCompression=yes
Uninstallable=no
VersionInfoTextVersion={#MCVersion}|{#JarIris}|{#JarSodium}
DefaultDirName={#DefaultRuntime}\jre-x64\bin
PrivilegesRequired=lowest
EnableDirDoesntExistWarning=False
DirExistsWarning=no

[ThirdParty]
UseRelativePaths=True

[Languages]
Name: "korean"; MessagesFile: "compiler:Languages\Korean.isl"

[CustomMessages]
needjava=Folder must contain java.exe(set automatically)
korean.needjava=java.exe가 포함된 폴더를 선택해야 합니다(자동으로 선택됨)
OptiDesc=OptiFabric(OptiFine)
korean.OptiDesc=옵티패브릭(옵티파인)
MiniDesc=Minimap
korean.MiniDesc=미니맵
VoiceDesc=Simple Voice Chat(Need Sever support)
korean.VoiceDesc=간단 보이스챗(서버 지원 필요)
caramelChat=caramelChat
korean.caramelChat=한글 입력
KoreanChat=Korean IME
korean.KoreanChat=한글 입력
Sodium=Sodium
korean.Sodium=소듐
Sodium2=Sodium
korean.Sodium2=소듐
Iris=Iris
korean.Iris=아이리스
Iris2=Iris
korean.Iris2=아이리스
IrisTest=Iris
korean.IrisTest=아이리스
Render=Renderer
korean.Render=렌더러
standard=standard
korean.standard=기본
custom=custom
korean.custom=사용자선택
Shader=Shader
korean.Shader=세이더
Debug=Debug
korean.Debug=디버그
MapMod=Map
korean.MapMod=지도
WorldDesc=World Map
korean.WorldDesc=월드맵
PerfMod=Performance
korean.PerfMod=성능
DeleteMods=Delete previous Mods
korean.DeleteMods=이전 모드 지우기
ModMenu=ModMenu
korean.ModMenu=모드 메뉴
MemoryFix=Memory leak Fix
korean.MemoryFix=메모리 누수 방지
Building=Building
korean.Building=건설
Effortless=Effortless
korean.Effortless=Effortless
ViewMod=View Mod
korean.ViewMod=뷰 모드
DistantHorizonsDesc=Distance Horizons
korean.DistantHorizonsDesc=Distance Horizons 장거리 렌더링
Indium=Indium
korean.Indium=인듐
VulkanMod=VulkanMod
korean.VulkanMod=VulkanMod
XaerosMapDesc=Xaeros
korean.XaerosMapDesc=Xaeros
JourneyMapDesc=JourneyMap
korean.JourneyMapDesc=JourneyMap
Animation=Animation
korean.Animation=애니메이션
EffectMod=Effect
korean.EffectMod=효과


[Types]
Name: "standard"; Description: "{cm:standard}"
Name: "custom"; Description: "{cm:custom}"; Flags: iscustom

[Components]
Name: "DeleteOldMod"; Description: "{cm:DeleteMods}"; Types: standard custom

#if Enable_IME_Chat!=0
;Name: "caramelChat"; Description: "{cm:caramelChat} {#JarCaramelchat}"; Types: standard custom
Name: "KoreanChat"; Description: "{cm:KoreanChat} {#JarKoreanIME}"; Types: standard custom
#endif

Name: "Renderer"; Description: "{cm:Render}"; Types: standard custom;
; sodium + iris 
#if Enable_RENDER_Sodium!=0
Name: "Renderer\Sodium2"; Description: "{cm:Sodium2} {#JarSodium}"; Types: standard custom

#if Enable_RENDER_Iris!=0
Name: "Renderer\Sodium2\Iris"; Description: "{cm:Iris2} {#JarIris}"; Types: standard custom;
#endif
#endif

#if Enable_MOD_Menu!=0
Name: "ModMenu"; Description: "{cm:ModMenu} {#JarModMenu}"; Types: standard custom;
#endif

#if Enable_UI_BetterF3!=0
Name: "BetterF3"; Description: "{cm:Debug} {#JarBetterF3}"; Types: custom
#endif


Name: "MapMod"; Description: "{cm:MapMod}"; Types: custom
#if Enable_MAP_journey!=0
Name: "MapMod\Journey"; Description: "{cm:JourneyMapDesc} {#JarJourneymap}"; Types: custom; Flags: exclusive
#endif
#if Enable_MAP_Xaeros!=0
Name: "MapMod\Xaeros"; Description: "{cm:XaerosMapDesc}"; Types: custom; Flags: exclusive
Name: "MapMod\Xaeros\Worldmap"; Description: "{cm:WorldDesc} {#JarXaerosWorldmap}"; Types: standard custom
Name: "MapMod\Xaeros\Minimap"; Description: "{cm:MiniDesc} {#JarXaerosMinimap}"; Types: standard custom
#endif

#if Enable_HoldMyItem!=0
Name: "Animation"; Description: "{cm:Animation}"; Types: custom
Name: "Animation\HoldMyItem"; Description: "{#JarHoldMyItem}"; Types: standard custom
#endif

#if Enable_PERF_Lithium!=0
Name: "PerfMod"; Description: "{cm:PerfMod}"; Types: custom
Name: "PerfMod\lithium"; Description: "{#JarLithium}"; Types: standard custom
#endif

#if Enable_LambDynamic!=0
Name: "Effect"; Description: "{cm:EffectMod}"; Types: standard custom
Name: "Effect\LambDynamic"; Description: "{#JarLambDynamic}"; Types: standard custom
#endif

#if Enable_Litematica!=0
Name: "Building"; Description: "{cm:Building}"; Types: custom
Name: "Building\Litematica"; Description: "{#JarLitematica}"; Types: custom
Name: "Building\Litematica\malilib"; Description: "{#JarMalilib}"; Types: custom;
#endif

;Name: "PerfMod\moreculling"; Description: "{#JarMoreculling}"; Types: custom

#if Enable_FIX_Memoryleak!=0
Name: "MemoryFix"; Description: "{cm:MemoryFix}"; Types: custom
Name: "MemoryFix\memoryleakfix"; Description: "{#JarMemoryleakfix}"; Types: custom
#endif

#if Enable_SHADER_Files!=0
Name: "Shader"; Description: "{cm:Shader}"; Types: standard custom;
Name: "Shader\Zip1"; Description: "{#ZipShaderSEUS}"; Types: standard custom;
Name: "Shader\Zip2"; Description: "{#ZipShaderBuilder}"; Types: standard custom;
Name: "Shader\Zip3"; Description: "{#ZipShaderSildur}"; Types: standard custom;
Name: "Shader\Zip4"; Description: "{#ZipShaderMakeup}"; Types: standard custom;
;Name: "Shader\Zip5"; Description: "{#ZipShaderBSL}"; Types: standard custom;
Name: "Shader\Zip6"; Description: "{#ZipShaderSolas}"; Types: standard custom;
;Name: "Shader\Zip7"; Description: "{#ZipShaderNoble}"; Types: standard custom;
Name: "Shader\Zip8"; Description: "{#ZipShaderVoyager}"; Types: standard custom;
;Name: "Shader\Zip9"; Description: "{#ZipShaderBliss} Support Distant Horizons"; Types: standard custom;
Name: "Shader\Zip10"; Description: "{#ZipShaderBeyond}"; Types: standard custom;
Name: "Shader\Zip11"; Description: "{#ZipShaderOpal}"; Types: standard custom;
Name: "Shader\Zip12"; Description: "{#ZipShaderComplementary}"; Types: standard custom;
#endif

#if Enable_VOICE_chat!=0
Name: "VoiceChat"; Description: "{cm:VoiceDesc} {#JarVoicechat}"; Types: custom
#endif

Name: "FabricAPI"; Description: "{#JarFabricAPI}"; Types: standard custom; Flags: fixed

#if Enable_BypassResource!=0
Name: "BypassResource"; Description: "{#JarBypassResource}"; Types: custom
#endif

[Code]
const
  OptiFineCmd='java -jar ';
  MCPFDir='{userappdata}\.minecraft\';
  ModsDir='Mods';
  PackDir='resourcepacks';
  NewMCDir='{#SetupSetting("DefaultDirName")}';
  MaxFindDeep=16;
  MaxFindFolder=5000;

var
  SMCDir: string;
  SJava: string;
  SMCPFDir: string;
  SModsDir: string;
  SPackDir: string;
  DirMsg: TLabel;
  FindRec: array[0..MaxFindDeep] of TFindRec;
  FindDeep: Integer;

function FindFilePath(path, filename:string):string;
var
  bf: Boolean;
  cPath: string;
  i, j: Integer;
begin
  if path='' then
    exit;
  FindDeep:=0;
  j:=0;
  cPath:='';
  bf:=FindFirst(path+'\*.*',FindRec[FindDeep]);
  if bf then begin
    try
    while bf do begin
      // skip '.' and '..'
      if (FindRec[FindDeep].Name<>'.') and (FindRec[FindDeep].Name<>'..') then begin
        if FindRec[FindDeep].Attributes and FILE_ATTRIBUTE_DIRECTORY<>0 then begin
          // folder
          Inc(j);
          if j>MaxFindFolder then
            break;
          if FindDeep<MaxFindDeep then begin
            cPath:='';
            for i:=0 to FindDeep do
              cPath:=cPath+'\'+FindRec[i].Name;
            Inc(FindDeep);
            bf:=FindFirst(path+cpath+'\*.*',FindRec[FindDeep]);
            if bf then
              continue;
          end;
        end else begin
          // file
          if CompareText(FindRec[FindDeep].Name,filename)=0 then begin
            cPath:=path;
            if FindDeep>0 then
              for i:=0 to FindDeep-1 do
                cPath:=cPath+'\'+FindRec[i].Name;
            Result:=cPath;
            break;
          end;
        end;
      end;
      // Next Folder
      bf:=FindNext(FindRec[FindDeep]);
      while not bf do begin
        FindClose(FindRec[FindDeep]);
        if FindDeep=0 then
          break;
        Dec(FindDeep);
        bf:=FindNext(FindRec[FindDeep]);
      end;
    end;
    finally
      // Free FindRec
      if FindDeep>0 then
        for i:=FindDeep downto 0 do
          FindClose(FindRec[FindDeep]);
    end;
  end;
end;

function MCDirCheck:string;
var
  SD, ST: string;
  SL: TStringList;
  i, j, k: Integer;
begin
  SD:=WizardForm.DirEdit.Text;
  ST:=FindFilePath(SD,'java.exe');
  if ST<>'' then
    Result:=ST
    else
    begin
      // scan log file
      SD:=ExpandConstant(MCPFDir)+'launcher_log.txt';
      if FileExists(SD) then 
      begin
        SL:=TStringList.Create;
        try
          SL.LoadFromFile(SD);
          if SL.Count>0 then
            for i:=0 to SL.Count-1 do
            begin
              // new beta version java
              j:=Pos('string:',SL[i]);
              if j>0 then
              begin
                k:=Pos('"',SL[i]);
                if k>0 then
                  j:=k+1
                  else
                    j:=j+7;
                ST:=Trim(Copy(SL[i],j,1024));
                j:=Pos('.exe',ST);
                if j>0 then
                begin
                  ST:=Copy(ST,1,j+4);
                  ST:=ExtractFileDir(ST);
                end else
                  ST:='';
              end;
              // old version java runtime
              j:=Pos('Java dir:',SL[i]);
              if j>0 then
              begin
                SD:=Trim(Copy(SL[i],j+9,1024))+'\bin';
                if ST<>'' then
                  break;
              end;
            end;
        finally
          SL.Free;
        end;
        Result:=FindFilePath(SD,'java.exe');
      end else
        Result:='';
      // use installed JRE path
      if Result='' then
      begin
        SD:=GetEnv('JAVA_HOME');
        if (SD<>'')then
          Result:=FindFilePath(SD,'java.exe');
      end;
      // new launcher has java beta version
      if Result='' then
      begin
        // use launcher folder
        if ST<>'' then
          Result:=FindFilePath(ST,'java.exe');
        if Result='' then
          Result:=FindFilePath(ExpandConstant('{#DefaultRuntime}'),'java.exe');
        if Result='' then
          Result:=FindFilePath(ExpandConstant('{commonpf32}'),'java.exe');
        if Result='' then
          Result:=FindFilePath(ExpandConstant('{commonpf}'),'java.exe');
      end;
    end;
end;

function MCJavaCheck:string;
var
  SD: string;
begin
  SD:=SMCDir+'\java.exe';
  if FileExists(SD) then
    Result:=SD
    else
      Result:='';
end;

function MCPFDirCheck:string;
var
  SD: string;
begin
  SD:=ExpandConstant(MCPFDir);
  if DirExists(SD) then
    Result:=SD
    else
      Result:='';
end;

procedure DirEdit_Change(Sender: TObject);
begin
  SMCDir:=MCDirCheck;
  if SMCDir='' then
    DirMsg.Font.Color:=clRed
    else
      DirMsg.Font.Color:=clGreen;
  SJava:=MCJavaCheck;
  if SJava<>'' then
    WizardForm.DirEdit.Text:=SMCDir;
end;

procedure ComponentsListClickCheck(Sender: TObject);
begin
  // always check required mod
  if WizardIsComponentSelected('Building\Litematica') then
    WizardSelectComponents('*Building\Litematica\malilib');
end;

procedure InitializeWizard;
begin
  WizardForm.DirEdit.OnChange:=@DirEdit_Change;
  WizardForm.ComponentsList.OnClickCheck:=@ComponentsListClickCheck;
end;

procedure CurPageChanged(CurPageID: Integer);
begin
  if CurPageID=wpReady then begin
    if (SMCDir='') or (SJava='') or (SMCPFDir='') or (SModsDir='') or (SPackDir='') then
      WizardForm.NextButton.Enabled:=False;
  end else
  // check and fill folder
  if CurPageID=wpSelectDir then begin
    // add label
    WizardForm.DirEdit.Text:=ExpandConstant(NewMCDir);
    if DirMsg=nil then begin
      DirMsg:=TLabel.Create(WizardForm);
      DirMsg.Parent:=WizardForm.SelectDirPage;
    end;
    DirMsg.Top:=ScaleY(110);
    DirMsg.Caption:=CustomMessage('needjava');

    DirEdit_Change(nil);
  end else
  if CurPageID=wpInstalling then
  begin
    if WizardIsComponentSelected('DeleteOldMod') then 
    begin
      DelTree(SModsDir+'\*.jar',False,True,False);
    end;
  end;
end;

function UpdateReadyMemo(Space, NewLine, MemoUserInfoInfo, MemoDirInfo, MemoTypeInfo, MemoComponentsInfo, MemoGroupInfo, MemoTasksInfo: String): String;
begin
  Result:='{#MCVersion} {#JarIris} {#JarSodium} {#CurrDate}'+#13#10;
  SMCDir:=MCDirCheck;
  Result:=Result+SMCDir;
  if SMCDir<>'' then
    Result:=Result+' - ok'
    else
      Result:=Result+' No Java Runtime';
  Result:=Result+#13#10;
  SJava:=MCJavaCheck;
  Result:=Result+SJava;
  if SJava<>'' then
    Result:=Result+' - ok'
    else
      Result:=Result+' No java.exe';
  Result:=Result+#13#10;
  SMCPFDir:=MCPFDirCheck;
  Result:=Result+SMCPFDir;
  if SMCPFDir<>'' then
    Result:=Result+' - ok'
    else
      Result:=Result+' No Minecraft user data';
  Result:=Result+#13#10;
  SModsDir:=SMCPFDir+ModsDir;
  SPackDir:=SMCPFDir+PackDir;
  CreateDir(SModsDir);
  if DirExists(SModsDir) then
    Result:=Result+'Access Mods Folder - ok'
      else begin
        Result:=Result+'Cannot access Mods folder';
        SModsDir:='';
      end;
  Result:=Result+#13#10;
end;

function GetOutDir(dummy:string):string;
begin
  Result:=SModsDir;
end;

function GetPackDir(dummy:string):string;
begin
  Result:=SPackDir;
end;

function JavaExec(dummy: string):string;
begin
  Result:=SJava;
end;

function GetFabricFile(dummy:string):string;
begin
  Result:=' -jar '+ExpandConstant('{tmp}')+'\'+'{#JarFabricInstaller}.jar';
end;

function GetShaderDir(dummy:string):string;
begin
  Result:=SMCPFDir+'shaderpacks';
  CreateDir(Result);
end;


[Files]
; Install Fabric
Source: "{#JarFabricInstaller}.jar"; DestDir: "{tmp}"; Flags: ignoreversion

; Mod Fabric API
Source: "{#JarFabricAPI}.jar"; DestDir: "{code:GetOutDir}"; Components: FabricAPI; Flags: ignoreversion

#if Enable_MAP_journey!=0
; Journeymap
Source: "{#JarJourneymap}.jar"; DestDir: "{code:GetOutDir}"; Components: MapMod\Journey; Flags: ignoreversion
#endif

#if Enable_MAP_Xaeros!=0
; Xaeros Mod minimap
Source: "{#JarXaerosMinimap}.jar"; DestDir: "{code:GetOutDir}"; Components: MapMod\Xaeros\Minimap; Flags: ignoreversion
; Xaeros Mod worldmap
Source: "{#JarXaerosWorldmap}.jar"; DestDir: "{code:GetOutDir}"; Components: MapMod\Xaeros\Worldmap; Flags: ignoreversion
#endif

; Simple Voice chat
#if Enable_VOICE_chat!=0
Source: "{#JarVoicechat}.jar"; DestDir: "{code:GetOutDir}"; Components: VoiceChat; Flags: ignoreversion
#endif

; caramelChat
#if Enable_IME_Chat!=0
;Source: "{#JarCaramelchat}.jar"; DestDir: "{code:GetOutDir}"; Components: caramelChat; Flags: ignoreversion
Source: "{#JarKoreanIME}.jar"; DestDir: "{code:GetOutDir}"; Components: KoreanChat; Flags: ignoreversion
#endif

; Sodium + iris
#if Enable_RENDER_Sodium!=0
Source: "{#JarSodium}.jar"; DestDir: "{code:GetOutDir}"; Components: Renderer\Sodium2; Flags: ignoreversion
#if Enable_RENDER_Iris!=0
Source: "{#JarIris}.jar"; DestDir: "{code:GetOutDir}"; Components: Renderer\Sodium2\Iris; Flags: ignoreversion
;Source: "{#JarMoreculling}.jar"; DestDir: "{code:GetOutDir}"; Components: Renderer\Sodium2\Iris1; Flags: ignoreversion
#endif
#endif

; vulkan mod
;Source: "{#JarVulkan}.jar"; DestDir: "{code:GetOutDir}"; Components: Renderer\VulkanMod; Flags: ignoreversion

; memory leak fix
#if Enable_FIX_Memoryleak!=0
Source: "{#JarMemoryleakfix}.jar"; DestDir: "{code:GetOutDir}"; Components: MemoryFix\memoryleakfix; Flags: ignoreversion
#endif

; Debug mod
#if Enable_UI_BetterF3!=0
Source: "{#JarBetterF3}.jar"; DestDir: "{code:GetOutDir}"; Components: BetterF3; Flags: ignoreversion
#endif

; performance
#if Enable_PERF_Lithium!=0
Source: "{#JarLithium}.jar"; DestDir: "{code:GetOutDir}"; Components: PerfMod\lithium; Flags: ignoreversion
#endif

#if Enable_BypassResource!=0
Source: "{#JarBypassResource}.jar"; DestDir: "{code:GetOutDir}"; Components: BypassResource; Flags: ignoreversion
#endif

; Mod Menu
#if Enable_MOD_Menu!=0
Source: "{#JarModMenu}.jar"; DestDir: "{code:GetOutDir}"; Components: ModMenu; Flags: ignoreversion
#endif

#if Enable_Litematica!=0
Source: "{#JarMalilib}.jar"; DestDir: "{code:GetOutDir}"; Components: Building\Litematica\malilib; Flags: ignoreversion; Check: WizardIsComponentSelected('Building\Litematica')
Source: "{#JarLitematica}.jar"; DestDir: "{code:GetOutDir}"; Components: Building\Litematica; Flags: ignoreversion
#endif

#if Enable_HoldMyItem!=0
Source: "{#JarHoldMyItem}.jar"; DestDir: "{code:GetOutDir}"; Components: Animation\HoldMyItem; Flags: ignoreversion
#endif

#if Enable_LambDynamic!=0
Source: "{#JarLambDynamic}.jar"; DestDir: "{code:GetOutDir}"; Components: Effect\LambDynamic; Flags: ignoreversion
#endif

#if Enable_SHADER_Files!=0
Source: "{#ZipShaderSEUS}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip1; Flags: ignoreversion
Source: "{#ZipShaderBuilder}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip2; Flags: ignoreversion
Source: "{#ZipShaderSildur}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip3; Flags: ignoreversion
Source: "{#ZipShaderMakeup}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip4; Flags: ignoreversion
;Source: "{#ZipShaderBSL}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip5; Flags: ignoreversion
Source: "{#ZipShaderSolas}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip6; Flags: ignoreversion
;Source: "{#ZipShaderNoble}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip7; Flags: ignoreversion
Source: "{#ZipShaderVoyager}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip8; Flags: ignoreversion
;Source: "{#ZipShaderBliss}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip9; Flags: ignoreversion
Source: "{#ZipShaderBeyond}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip10; Flags: ignoreversion
Source: "{#ZipShaderOpal}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip11; Flags: ignoreversion
Source: "{#ZipShaderComplementary}.zip"; DestDir: "{code:GetShaderDir}"; Components: Shader\Zip12; Flags: ignoreversion
#endif

[Run]
Filename: "{code:JavaExec}"; Parameters: "{code:GetFabricFile} client -mcversion {#MCVersion} ";

